/*
Copyright (c) 2014 Barend Garvelink

SebastiaanSchool is licensed under a
Creative Commons Attribution-NonCommercial 3.0 Unported License.

You should have received a copy of the license along with this
work.  If not, see <http://creativecommons.org/licenses/by-nc/3.0/>.
*/

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile file('proguard-shrink-only.txt')
            buildConfigField "String", "ENDPOINT_URL", "\"https://backend-sebastiaanschool.rhcloud.com/\""
        }
        debug {
            minifyEnabled false
            proguardFile file('proguard-shrink-only.txt')
            buildConfigField "String", "ENDPOINT_URL", "\"http://${getEnv('SEBASTIAAN_HOST', '10.0.2.2')}:8000/\""
        }
    }

    defaultConfig {
        applicationId "nl.sebastiaanschool.contact.app"
        versionCode 5
        versionName "2.0.0"
        minSdkVersion 14
        targetSdkVersion 24
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfigs "en", "nl"
        vectorDrawables.useSupportLibrary = true
    }

    android {
        lintOptions {
            disable 'InvalidPackage',          // This triggers on some files in Retrofit and Okio
                    'GoogleAppIndexingWarning' // We're not interested in App Indexing
        }
    }
}

def getEnv(key, defVal = '') {
    System.env.containsKey(key) ? System.env.get(key) : defVal
}

// Import code signing configuration from a separate file, outside version control.
def extraConf = file('signing-config.gradle');
if (extraConf.exists()) {
    apply from: 'signing-config.gradle'
}

project.ext {
    retrofitVersion = '2.1.0'
    supportLibVersion = '24.2.0'
    testSupportVersion = '2.2.2'
    firebaseVersion = '9.4.0'
}

dependencies {
    compile "com.android.support:support-v4:${project.ext.supportLibVersion}"
    compile "com.android.support:appcompat-v7:${project.ext.supportLibVersion}"
    compile "com.android.support:cardview-v7:${project.ext.supportLibVersion}"
    compile "com.android.support:preference-v7:${project.ext.supportLibVersion}"
    compile "com.android.support:design:${project.ext.supportLibVersion}"
    compile "com.android.support:support-annotations:${project.ext.supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${project.ext.supportLibVersion}"
    compile "com.android.support:support-vector-drawable:${project.ext.supportLibVersion}"
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.9'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile "com.squareup.retrofit2:retrofit:${project.ext.retrofitVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava:${project.ext.retrofitVersion}"
    compile "com.squareup.retrofit2:converter-moshi:${project.ext.retrofitVersion}"
    compile 'com.squareup.moshi:moshi:1.2.0'
    compile 'net.danlew:android.joda:2.9.4.1'
    compile "com.google.firebase:firebase-crash:${project.ext.firebaseVersion}"
    testCompile 'junit:junit:4.12'
    androidTestCompile "com.android.support.test.espresso:espresso-core:${project.ext.testSupportVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:${project.ext.testSupportVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-idling-resource:${project.ext.testSupportVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:${project.ext.testSupportVersion}"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support:support-annotations:${project.ext.supportLibVersion}"
    androidTestCompile "com.android.support:support-v4:${project.ext.supportLibVersion}"
    androidTestCompile "com.android.support:appcompat-v7:${project.ext.supportLibVersion}"
    androidTestCompile "com.android.support:design:${project.ext.supportLibVersion}"
    androidTestCompile "com.android.support:recyclerview-v7:${project.ext.supportLibVersion}"
}

apply plugin: 'com.google.gms.google-services'
