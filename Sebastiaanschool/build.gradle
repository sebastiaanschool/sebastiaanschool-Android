buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.0"

    buildTypes {
        release {
            runProguard true
            proguardFile file('proguard-shrink-only.txt')
            project.ext.set('parseAppIdVar', 'PARSE_APPLICATION_ID')
            project.ext.set('parseClientKeyVar', 'PARSE_CLIENT_KEY')
        }
        debug {
            runProguard true
            proguardFile file('proguard-shrink-only.txt')
            project.ext.set('parseAppIdVar', 'DEV_PARSE_APPLICATION_ID')
            project.ext.set('parseClientKeyVar', 'DEV_PARSE_CLIENT_KEY')
        }
    }

    productFlavors {
        parse
        stubbed {
            packageName "nl.sebastiaanschool.contact.app.stubbed"
        }
    }
}

/*
 The next bit of code adds a doLast to the generateParse{Debug|Release}BuildConfig tasks to
 generate a ParseConfig.java file alongside the BuildConfig.java file. I'm pretty sure there
 must be a better way to tie it together than by responding to the whenTaskAdded event, but
 this will do for now.
 */
tasks.whenTaskAdded { theTask ->
    if (theTask.name.startsWith('generateParse') && theTask.name.endsWith('BuildConfig')) {
        theTask << {
            def parseAppId = System.env.get(project.ext.parseAppIdVar)
            def parseClientKey = System.env.get(project.ext.parseClientKeyVar)
            parseAppId = parseAppId == null ? "null" : "\"${parseAppId}\""
            parseClientKey = parseClientKey == null ? "null" : "\"${parseClientKey}\""
            if (parseAppId == "null" || parseClientKey == "null") {
                logger.warn("*****\n\nMissing Parse.com API credentials. The generated app will be non-functional.\n\nTo resolve this, please define the environment variables ${project.ext.parseAppIdVar} and ${project.ext.parseClientKeyVar}.\n\n*****")
            }
            def theFile = new java.io.File(it.sourceOutputDir, "nl/sebastiaanschool/contact/app/ParseConfig.java")
            if (theFile.exists()) {
                theFile.delete()
            }
            theFile.append """
            package nl.sebastiaanschool.contact.app;

            // The strings in this file are populated during build with the values of the
            // PARSE_APPLICATION_ID and PARSE_CLIENT_KEY environment variables. If they're
            // null below, your build env was not set correctly (check the readme file).
            class ParseConfig {
                static final String APPLICATION_ID = ${parseAppId};
                static final String CLIENT_KEY = ${parseClientKey};
            }
            """.stripIndent();
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:+'
    compile files('libs/Parse-1.3.9.jar')
}
