buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.0"

    productFlavors {
        parse
        stubbed {
            packageName "nl.sebastiaanschool.contact.app.stubbed"
        }
    }
}

/*
 The next bit of code adds a doLast to the generateParse{Debug|Release}BuildConfig tasks to
 generate a ParseConfig.java file alongside the BuildConfig.java file. I'm pretty sure there
 must be a better way to tie it together than by responding to the whenTaskAdded event, but
 this will do for now.
 */
tasks.whenTaskAdded { theTask ->
    if (theTask.name.startsWith('generateParse') && theTask.name.endsWith('BuildConfig')) {
        theTask << {
            if (System.env.PARSE_APPLICATION_ID == null || System.env.PARSE_CLIENT_KEY == null) {
                throw new RuntimeException("Error: missing Parse.com API credentials. Cannot not assemble the app for the parse.com back-end.\n\nPlease define the environment variables PARSE_APPLICATION_ID and PARSE_CLIENT_KEY.")
            }
            def theFile = new java.io.File(it.sourceOutputDir, "nl/sebastiaanschool/contact/app/ParseConfig.java")
            if (theFile.exists()) {
                theFile.delete()
            }
            theFile.append """
            package nl.sebastiaanschool.contact.app;

            // The strings in this file are populated during build with the values of the
            // PARSE_APPLICATION_ID and PARSE_CLIENT_KEY environment variables.
            class ParseConfig {
                static final String APPLICATION_ID = "${System.env.PARSE_APPLICATION_ID}";
                static final String CLIENT_KEY = "${System.env.PARSE_CLIENT_KEY}";
            }
            """.stripIndent();
        }
    }
}

dependencies {
//    compile 'com.android.support:support-v4:+'
//    compile 'com.android.support:support-v13:+'
    compile files('libs/Parse-1.3.5.jar')
}
