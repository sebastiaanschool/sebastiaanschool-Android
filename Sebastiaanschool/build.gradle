/*
Copyright (c) 2014 Barend Garvelink

SebastiaanSchool is licensed under a
Creative Commons Attribution-NonCommercial 3.0 Unported License.

You should have received a copy of the license along with this
work.  If not, see <http://creativecommons.org/licenses/by-nc/3.0/>.
*/

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile file('proguard-shrink-only.txt')
            buildConfigField "String", "PARSE_APPLICATION_ID", getEnv('PARSE_APPLICATION_ID')
            buildConfigField "String", "PARSE_CLIENT_KEY", getEnv('PARSE_CLIENT_KEY')
        }
        debug {
            minifyEnabled false
            proguardFile file('proguard-shrink-only.txt')
            buildConfigField "String", "PARSE_APPLICATION_ID", getEnv('DEV_PARSE_APPLICATION_ID')
            buildConfigField "String", "PARSE_CLIENT_KEY", getEnv('DEV_PARSE_CLIENT_KEY')
        }
    }

    defaultConfig {
        versionCode 5
        versionName "1.3.0"
        minSdkVersion 14
        targetSdkVersion 23
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

def getEnv(key, defVal = '') {
    "\"${System.env.containsKey(key) ? System.env.get(key) : defVal}\""
}

// Import code signing configuration from a separate file, outside version control.
def extraConf = file('signing-config.gradle');
if (extraConf.exists()) {
    apply from: 'signing-config.gradle'
}

project.ext.supportLibVersion = "23.2.1"

dependencies {
    compile "com.android.support:support-v4:${project.ext.supportLibVersion}"
    compile "com.android.support:appcompat-v7:${project.ext.supportLibVersion}"
    compile "com.android.support:cardview-v7:${project.ext.supportLibVersion}"
    compile "com.android.support:gridlayout-v7:${project.ext.supportLibVersion}"
    compile "com.android.support:design:${project.ext.supportLibVersion}"
    compile "com.android.support:support-annotations:${project.ext.supportLibVersion}"
    compile files('libs/Parse-1.9.3.jar')
    androidTestCompile "com.android.support.test.espresso:espresso-core:2.2.2"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:2.2.2"
    androidTestCompile "com.android.support.test.espresso:espresso-idling-resource:2.2.2"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:2.2.2"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support:support-annotations:${project.ext.supportLibVersion}"
    compile 'com.parse.bolts:bolts-android:1.2.0'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
}
